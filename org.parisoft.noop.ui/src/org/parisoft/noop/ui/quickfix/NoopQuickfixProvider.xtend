/*
 * generated by Xtext 2.10.0
 */
package org.parisoft.noop.ui.quickfix

import org.eclipse.xtext.ui.editor.quickfix.DefaultQuickfixProvider
import org.eclipse.xtext.ui.editor.quickfix.IssueResolutionAcceptor
import org.eclipse.xtext.validation.Issue
import org.parisoft.noop.noop.NoopClass
import org.eclipse.xtext.ui.editor.quickfix.Fix
import org.parisoft.noop.validation.NoopValidator
import org.parisoft.noop.noop.StorageType
import org.parisoft.noop.noop.Variable
import org.parisoft.noop.noop.NoopFactory
import java.util.regex.Pattern
import org.parisoft.noop.^extension.Members
import org.parisoft.noop.noop.Method

/**
 * Custom quickfixes.
 * 
 * See https://www.eclipse.org/Xtext/documentation/310_eclipse_support.html#quick-fixes
 */
class NoopQuickfixProvider extends DefaultQuickfixProvider {

	val camelCasePattern = Pattern::compile('(?=[A-Z])')
	
//	@Fix(NoopValidator.INVALID_NAME)
//	def capitalizeName(Issue issue, IssueResolutionAcceptor acceptor) {
//		acceptor.accept(issue, 'Capitalize name', 'Capitalize the name.', 'upcase.png') [
//			context |
//			val xtextDocument = context.xtextDocument
//			val firstLetter = xtextDocument.get(issue.offset, 1)
//			xtextDocument.replace(issue.offset, 1, firstLetter.toUpperCase)
//		]
//	}
	@Fix(NoopValidator::CLASS_RECURSIVE_HIERARCHY)
	def classRecursiveHierarchy(Issue issue, IssueResolutionAcceptor acceptor) {
		val desc = '''
		«issue.data.head» {
			...
		}'''
		acceptor.accept(issue, 'Remove hierarchy', desc, null, [ c, ctx |
			(c as NoopClass).superClass = null
		])
	}
	
	@Fix(NoopValidator::METHOD_IRQ_NON_STATIC)
	def methodIrqNonStatic(Issue issue, IssueResolutionAcceptor acceptor) {
		val name = issue.data.head
		val static = Members::STATIC_PREFIX + name
		val label = '''Make «name» static'''
		val desc = '''
		«static»() «issue.data.get(1)» {
			...
		}'''
		acceptor.acceptMulti(issue, label, desc, null, [Method m|
			m.name = static
		])
	}
	
	@Fix(NoopValidator::METHOD_IRQ_PARAMS)
	def methodIrqParams(Issue issue, IssueResolutionAcceptor acceptor) {
		val name = issue.data.head
		val label = '''Remove «name» parameters'''
		val desc = '''
		«name»() «issue.data.get(1)» {
			...
		}
		'''
		acceptor.acceptMulti(issue, label, desc, null, [Method m|
			m.params.clear
		])
	}
	
	@Fix(NoopValidator::METHOD_STORAGE_TYPE)
	def methodStorageType(Issue issue, IssueResolutionAcceptor acceptor) {
		val name = issue.data.head
		val label = '''Remove «issue.data.get(1)» tag'''
		val desc = '''
		«name»(...) {
			...
		}
		'''
		acceptor.accept(issue, label, desc, null, [o, ctx|
			(o as Method).storage = null
		])
	}

	@Fix(NoopValidator::STRING_FILE_NON_ROM)
	def stringFileNonRom(Issue issue, IssueResolutionAcceptor acceptor) {
		val prgDesc = '''«issue.data.head» «StorageType::PRGROM.literal»'''
		val chrDesc = '''«issue.data.head» «StorageType::CHRROM.literal»'''
		acceptor.accept(issue, '''Tag as «StorageType::PRGROM.literal»''', prgDesc, null, [ o, ctx |
			(o as Variable).storage = NoopFactory::eINSTANCE.createStorage => [type = StorageType::PRGROM]
		])
		acceptor.accept(issue, '''Tag as «StorageType::CHRROM.literal»''', chrDesc, null, [ o, ctx |
			(o as Variable).storage = NoopFactory::eINSTANCE.createStorage => [type = StorageType::CHRROM]
		])
	}
	
	@Fix(NoopValidator::ROM_FIELD_NON_CONSTANT)
	def romFieldNonConstant(Issue issue, IssueResolutionAcceptor acceptor) {
		val name = issue.data.head
		val const = '''«IF !name.contains(Members::STATIC_PREFIX)»«Members::STATIC_PREFIX»«ENDIF»«camelCasePattern.split(name).join('_').toUpperCase»'''
		val label = '''Make «name» constant'''
		val desc = '''«const» «issue.data.get(1)»'''
		acceptor.acceptMulti(issue, label, desc, null, [Variable v|
			v.name = const
		])
	}
	
	@Fix(NoopValidator::INES_FIELD_NON_CONSTANT)
	def inesFieldNonConstant(Issue issue, IssueResolutionAcceptor acceptor) {
		val name = issue.data.head
		val const = '''«IF !name.contains(Members::STATIC_PREFIX)»«Members::STATIC_PREFIX»«ENDIF»«camelCasePattern.split(name).join('_').toUpperCase»'''
		val label = '''Make «name» constant'''
		val desc = '''«const» «issue.data.get(1)»'''
		acceptor.acceptMulti(issue, label, desc, null, [Variable v|
			v.name = const
		])
	}
	
	@Fix(NoopValidator::CONSTANT_FIELD_DIMENSION)
	def constantFieldDimension(Issue issue, IssueResolutionAcceptor acceptor) {
		val prgDesc = '''«issue.data.head» «StorageType::PRGROM.literal»'''
		val chrDesc = '''«issue.data.head» «StorageType::CHRROM.literal»'''
		acceptor.accept(issue, '''Tag as «StorageType::PRGROM.literal»''', prgDesc, null, [ o, ctx |
			(o as Variable).storage = NoopFactory::eINSTANCE.createStorage => [type = StorageType::PRGROM]
		])
		acceptor.accept(issue, '''Tag as «StorageType::CHRROM.literal»''', chrDesc, null, [ o, ctx |
			(o as Variable).storage = NoopFactory::eINSTANCE.createStorage => [type = StorageType::CHRROM]
		])
	}
	
	@Fix(NoopValidator::CONSTANT_FIELD_STORAGE)
	def constantFieldStorage(Issue issue, IssueResolutionAcceptor acceptor) {
		val label = '''Remove «issue.data.get(1)» tag'''
		val desc = issue.data.head
		acceptor.accept(issue, label, desc, null, [o, ctx|
			(o as Variable).storage = null
		])
	}
}
